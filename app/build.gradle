buildscript {
    ext.kotlin_version = '1.0.0-beta-4589'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:$kotlin_version"
    }
}

plugins {
    id 'com.github.hierynomus.license' version '0.12.1'
    id 'net.researchgate.release' version '2.3.5'
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "alfio.backoffice"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 2
        versionName "1.0-alpha"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main {
            assets.srcDirs = ['src/main/assets', 'build/reports/license']
            java {
                srcDir 'src/main/kotlin'
            }
        }
    }
    lintOptions {
        // https://github.com/square/okio/issues/58
        warning 'InvalidPackage'
        abortOnError false
    }
    signingConfigs {
        release {
            def propertiesFile = new File("./signing_config")
            final properties = new Properties();
            if (propertiesFile.exists()) {
                propertiesFile.withReader { properties.load(it) }
                storeFile properties['ALFIO_KEYSTORE_PATH']
                storePassword properties['ALFIO_KEYSTORE_PWD']
                keyAlias "alf.io"
                keyPassword properties['ALFIO_KEY_PWD']
            }
        }
    }
}

downloadLicenses {
    includeProjectDependencies = true
    reportByDependency = true
    reportByLicenseType = false
    report.html.enabled = false
    report.xml.enabled = true
    dependencyConfiguration = "compile"
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    // GUI: Third-party libraries
    // ZXING: https://github.com/journeyapps/zxing-android-embedded
    testCompile 'junit:junit:4.12'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'
    compile 'com.android.support:cardview-v7:23.1.1'
    compile 'com.android.support:recyclerview-v7:23.1.1'
    compile 'com.android.support:preference-v7:23.1.1'
    compile 'com.android.support:support-v4:23.1.1'
    compile 'com.google.code.gson:gson:2.4'
    compile 'com.github.clans:fab:1.6.1'
    compile 'com.journeyapps:zxing-android-embedded:3.1.0@aar'
    compile 'com.squareup.okhttp:okhttp:2.7.1'
    compile 'com.google.zxing:core:3.2.1'
}

license {
    header = rootProject.file('config/HEADER')
    strictCheck = true
    ignoreFailures = false
    mapping {
        java = 'JAVADOC_STYLE'
        kt = 'JAVADOC_STYLE'
    }
    ext.year = '2015-' + GregorianCalendar.newInstance().get(Calendar.YEAR);
    include "**/*.kt"
}

//http://stackoverflow.com/a/31963059
project.afterEvaluate {
    mergeDebugAssets.dependsOn project.tasks.getByName('downloadLicenses')
    mergeReleaseAssets.dependsOn project.tasks.getByName('downloadLicenses')
}

release {
    git {
        requireBranch = ''
        pushToRemote = 'origin'
    }
}

repositories {
    mavenCentral()
}
