import java.text.SimpleDateFormat

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:$kotlin_version"
    }
}

plugins {
    id 'com.github.hierynomus.license' version '0.14.0'
    id 'net.researchgate.release' version '2.6.0'
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.3'

    defaultConfig {
        applicationId "alfio.backoffice"
        minSdkVersion 15
        targetSdkVersion 25
        versionCode Integer.parseInt(new SimpleDateFormat("yyMMddHHmm").format(new Date()))
        versionName project.version
    }
    signingConfigs {
        def propertiesFile = new File("app/signing_config")
        final properties = new Properties();
        propertiesFile.withReader { properties.load(it) }
        release {
            storeFile file(properties['ALFIO_KEYSTORE_PATH'])
            storePassword properties['ALFIO_KEYSTORE_PWD']
            keyAlias "alf.io"
            keyPassword properties['ALFIO_KEY_PWD']
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    sourceSets {
        main {
            assets.srcDirs = ['src/main/assets', 'build/reports/license']
            java {
                srcDir 'src/main/kotlin'
            }
        }
    }
    lintOptions {
        // https://github.com/square/okio/issues/58
        warning 'InvalidPackage'
        abortOnError false
    }
}

downloadLicenses {
    includeProjectDependencies = true
    reportByDependency = true
    reportByLicenseType = false
    report.html.enabled = false
    report.xml.enabled = true
    dependencyConfiguration = "compile"
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    // GUI: Third-party libraries
    // ZXING: https://github.com/journeyapps/zxing-android-embedded
    testCompile 'junit:junit:4.12'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    testCompile("com.nhaarman:mockito-kotlin:0.6.2") {
        exclude group: 'org.jetbrains.kotlin'
    }
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'
    compile 'com.android.support:cardview-v7:25.3.1'
    compile 'com.android.support:recyclerview-v7:25.3.1'
    compile 'com.android.support:preference-v7:25.3.1'
    compile 'com.android.support:support-v4:25.3.1'
    compile 'com.google.code.gson:gson:2.8.0'
    compile 'com.github.clans:fab:1.6.4'
    compile 'com.journeyapps:zxing-android-embedded:3.5.0@aar'
    compile 'com.squareup.okhttp3:okhttp:3.7.0'
    compile 'com.google.zxing:core:3.3.0'
}

license {
    header = rootProject.file('config/HEADER')
    strictCheck = true
    ignoreFailures = false
    mapping {
        java = 'JAVADOC_STYLE'
        kt = 'JAVADOC_STYLE'
    }
    ext.year = '2015-' + GregorianCalendar.newInstance().get(Calendar.YEAR);
    include "**/*.kt"
}

//http://stackoverflow.com/a/31963059
project.afterEvaluate {
    mergeDebugAssets.dependsOn project.tasks.getByName('downloadLicenses')
    mergeReleaseAssets.dependsOn project.tasks.getByName('downloadLicenses')
}

release {
    buildTasks = ['build']
    git {
        requireBranch = ''
        pushToRemote = 'origin'
    }
}

repositories {
    mavenCentral()
}
